import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.internal.os.OperatingSystem

plugins {
  id 'java'
  id 'org.openjfx.javafxplugin' version '0.1.0'
  id 'java-library'
  id 'maven-publish'
}

project.ext.hash = System.getenv("GITHUB_SHA")

if(project.ext.hash == null) {
  project.ext.hash = 'snapshot'
}

group = 'legend'
version = project.ext.hash

java {
  sourceCompatibility = targetCompatibility = JavaVersion.VERSION_21
}

compileJava.options.encoding = 'UTF-8'

project.ext.lwjglVersion = "3.4.0-SNAPSHOT"
project.ext.jomlVersion = "1.10.8"

final def operatingSystem = hasProperty("os") ? OperatingSystem.forName(os) : OperatingSystem.current()
final def architecture = hasProperty("arch") ? arch : System.getProperty("os.arch")
final def isArm = architecture.startsWith("arm") || architecture.startsWith("aarch64")
final def isSteamDeck = hasProperty("steamdeck") && "true" == steamdeck

switch(operatingSystem) {
  case OperatingSystem.LINUX:
    project.ext.lwjglNatives = "natives-linux"
    break

  case OperatingSystem.MAC_OS:
    project.ext.lwjglNatives = "natives-macos"
    break

  case OperatingSystem.WINDOWS:
    project.ext.lwjglNatives = "natives-windows"
    break
}

if(isArm) {
  project.ext.lwjglNatives += "-arm64"
}

println "Building for " + operatingSystem.familyName + " " + architecture

repositories {
  mavenCentral()
  mavenLocal()
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
  api group: 'org.legendofdragoon', name: 'mod-loader', version: '4.2.0'
  api group: 'org.legendofdragoon', name: 'script-recompiler', version: '0.5.6'

  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.24.3'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.24.3'
  implementation group: 'org.fusesource.jansi', name: 'jansi', version: '2.4.1'

  api platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

  api "org.lwjgl:lwjgl"
  api "org.lwjgl:lwjgl-sdl"
  api "org.lwjgl:lwjgl-opengl"
  api "org.lwjgl:lwjgl-openal"
  api "org.lwjgl:lwjgl-opus"
  api "org.lwjgl:lwjgl-stb"
  runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
  runtimeOnly "org.lwjgl:lwjgl-sdl::$lwjglNatives"
  runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
  runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
  runtimeOnly "org.lwjgl:lwjgl-opus::$lwjglNatives"
  runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
  api "org.joml:joml:${jomlVersion}"

  implementation group: 'org.asynchttpclient', name: 'async-http-client', version: '3.0.1' // update check
  implementation group: 'org.json', name: 'json', version: '20250107'

  implementation group: 'com.opencsv', name: 'opencsv', version: '5.9'
  implementation group: 'io.github.java-diff-utils', name: 'java-diff-utils', version: '4.15'
  implementation group: 'commons-io', name: 'commons-io', version: '2.18.0'

  api group: 'com.github.slugify', name: 'slugify', version: '3.0.7'

  api group: 'it.unimi.dsi', name: 'fastutil', version: '8.5.15'

  implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

  implementation group: 'net.java.dev.jna', name: 'jna', version: '5.16.0'
}

javafx {
  version = "21.0.1"
  modules = ['javafx.controls', 'javafx.fxml']
}

if(hasProperty("os")) {
  javafx.platform = os + (!operatingSystem.windows && isArm ? "-aarch64" : "")
}

jar {
  manifest {
    attributes(
      'Main-Class': 'legend.game.Main'
    )
  }
}

tasks.register('sourceJar', Jar) {
  from sourceSets.main.allJava
  archiveClassifier = "sources"
}

tasks.register('clearOutput', Delete) {
  delete layout.buildDirectory.dir("libs/libs")
  delete layout.buildDirectory.file("libs/**")
}

tasks.register('copyLibs', Copy) {
  from configurations.compileClasspath + configurations.runtimeClasspath
  into layout.buildDirectory.dir("libs/libs")
}

tasks.register('copySupportFiles', Copy) {
  into layout.buildDirectory.dir("libs")

  into("gfx") {
    from 'gfx'
  }

  into("patches") {
    from 'patches'
  }

  into("lang") {
    from 'lang'
  }

  into(".") {
    switch(operatingSystem) {
      case OperatingSystem.LINUX:
        final var archives = configurations.compileClasspath + configurations.runtimeClasspath
        final var libs = "libs/" + archives*.name.join(":libs/")

        from(isSteamDeck ? 'launch-steamdeck' : 'launch-linux') {
          filter(ReplaceTokens, tokens: [version: project.ext.hash])
          filter(ReplaceTokens, tokens: [false: "true"])
          filter(ReplaceTokens, tokens: [libs: libs])
          rename("-\\w+", "")
        }
        from('download-java') {
          filter(ReplaceTokens, tokens: [os: 'linux', arch: isArm ? "aarch64" : "x64"])
        }
        break

      case OperatingSystem.MAC_OS:
        final var archives = configurations.compileClasspath + configurations.runtimeClasspath
        final var libs = "libs/" + archives*.name.join(":libs/")

        from('launch-macos') {
          filter(ReplaceTokens, tokens: [version: project.ext.hash])
          filter(ReplaceTokens, tokens: [false: "true"])
          filter(ReplaceTokens, tokens: [libs: libs])
          rename("-macos", "")
        }
        from('download-java') {
          filter(ReplaceTokens, tokens: [os: 'macosx', arch: isArm ? "aarch64" : "x64"])
        }
        break

      case OperatingSystem.WINDOWS:
        final var archives = configurations.compileClasspath + configurations.runtimeClasspath
        final var libs = "libs/" + archives*.name.join(";libs/")

        from('launch.bat') {
          filter(ReplaceTokens, tokens: [version: project.ext.hash])
          filter(ReplaceTokens, tokens: [false: "true"])
          filter(ReplaceTokens, tokens: [libs: libs])
        }
        break
    }

    from 'log4j2.xml'
    from 'gamecontrollerdb.txt'
    from 'README.md'
    from 'LICENSE'
  }
}

jar.dependsOn(clearOutput, copyLibs, copySupportFiles)

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = 'lod'
      from components.java
      pom {
        name = 'LoD Engine'
      }

      artifact sourceJar
    }
  }
}

test {
  useJUnitPlatform()
}

sourceSets {
  main {
    java {
      srcDirs = ["src/main/java"]
      exclude ".gradle", "build", "files"
    }
  }
}
